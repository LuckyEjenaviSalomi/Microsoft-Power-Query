(tbl as table)=>

let

// Function to remove empty columns
fxRemoveEmptyColumns = (tbl as table)=>
let
    colNames = Table.ColumnNames(tbl),
    ChangedTypeToText = Table.TransformColumnTypes(tbl, List.Transform(colNames, each {_, type text})),
    TrimmedText = Table.TransformColumns(ChangedTypeToText, List.Transform(colNames, each {_, Text.Trim, type text})),
    ReplacedValue = Table.ReplaceValue(TrimmedText,"",null,Replacer.ReplaceValue, colNames),
    replace_empties = Table.ReplaceValue(ReplacedValue," ", null, Replacer.ReplaceValue, colNames),
    GetTableProfile = Table.Profile(replace_empties),
    DrillDownToColumnNullCount = Table.SelectColumns(GetTableProfile,{"Column", "NullCount"}),
    row_count = Table.RowCount(tbl),
    GetAllNullCount = Table.SelectRows(DrillDownToColumnNullCount, each [NullCount] = row_count),
    ToRemoveColList = GetAllNullCount[Column]
in
    Table.RemoveColumns(tbl, ToRemoveColList),

// Function to remove empty rows
fxRemoveEmptyRows = (tbl as table)=>
    let
    Source = tbl,
    ChangedTypeToText = Table.TransformColumnTypes(Source, List.Transform(Table.ColumnNames(Source), each {_, type text})),
    TrimmedText = Table.TransformColumns(ChangedTypeToText, List.Transform(Table.ColumnNames(Source), each {_, Text.Trim, type text})),
    ReplacedValue = Table.ReplaceValue(TrimmedText,"",null,Replacer.ReplaceValue, Table.ColumnNames(Source)),
    replace_empties = Table.ReplaceValue(ReplacedValue," ", null, Replacer.ReplaceValue, Table.ColumnNames(ReplacedValue)),
        CountNonNullColumns = Table.AddColumn(replace_empties, "Custom", each List.NonNullCount(Record.FieldValues(_))), //Check the count of non-null columns
        RemoveNullRows = Table.SelectRows(CountNonNullColumns, each ([Custom] <> 0)) //Remove records with zero non-null count
        in Table.RemoveColumns(RemoveNullRows,{"Custom"})

// Final Result
in 
    fxRemoveEmptyRows(fxRemoveEmptyColumns(tbl))
