/*
    Function returns dynamic data types of Power Query Table.
    `tbl`: a table
    `exclude_cols`: list of column names to exclude from data type conversion
    `N_rows`: number of first rows to evaluate - defaulted to 10
*/

(tbl as table, optional exclude_cols as any, optional N_rows as nullable number)=>
let
    //remove exclude_cols
    ColNames = if Value.Is(exclude_cols, type list) then List.Difference(List.Buffer(Table.ColumnNames(tbl)), exclude_cols)
                else Table.ColumnNames(tbl),
    N_rows = (if N_rows = null then 100 else N_rows), //Select the first 100 rows if N_rows is not specified
    tbl_focus = Table.Buffer(Table.SelectColumns(tbl, ColNames)), //Focus table after excluding specified columns
    first_NRows = List.Buffer(Table.ToColumns(Table.FirstN(tbl_focus, N_rows))),
    RemoveNulls = List.Transform(first_NRows, each List.RemoveNulls(_)),
    RemoveNullZeros = List.Transform(RemoveNulls, each List.RemoveNulls(_)),
    GetFirstItem = List.Transform(RemoveNullZeros, each List.First(_)),
    TypeCastNumbers = List.Transform(GetFirstItem, each 
            if Value.Is(_, type date) then _ // Leave date untouched
            else
                //Test if number starts with 0 and not equal to 0, then leave as text, else convert to number
                try ( 
                    if Value.Is(Number.From(_), type number) and Text.StartsWith(_, "0") and Number.From(_) <> 0 then _ else Number.From(_) 
                    ) 
                otherwise (
                        try (if not Value.Is(_, type number) then Date.From(_) else Number.From(_) ) //Convert date in text format to date, e.g, 09.04.2024
                        otherwise _
                )
            ),

    ValueType = List.Transform(TypeCastNumbers, each if _ = null then type any else if Value.Is(_, type number) and Number.Mod(_, Int64.From(_)) = 0 then Int64.Type else Value.Type(_) ),
    ColNames_Types = List.Zip({ColNames, ValueType}),
    Result = Table.TransformColumnTypes(tbl, ColNames_Types)
in
    Result
